//IMPORTAR FINAL
//NETEJAR DATABASE
MATCH (n)
DETACH DELETE n;


//CONSTRAINTS
CREATE CONSTRAINT UNIQUE_ID IF NOT EXISTS FOR (i:INDIVIDUAL) REQUIRE i.Id is UNIQUE;

CREATE CONSTRAINT ID_INDIVIDUAL IF NOT EXISTS FOR (i:INDIVIDUAL) REQUIRE i.Id is NOT NULL;
CREATE CONSTRAINT ID_HABITATGE IF NOT EXISTS FOR (h:HABITATGE) REQUIRE h.Id_Habitatge is NOT NULL;

//INDEX
CREATE INDEX AnyPadroIndex IF NOT EXISTS FOR ()-[r:VIU]-() ON (r.Year);
CREATE INDEX RelacioFamilia IF NOT EXISTS FOR ()-[r:FAMILIA]-() ON (r.Relacio_Harmonitzada);


//HABITATGES
LOAD CSV WITH HEADERS FROM 'file:///HABITATGES.csv' AS row
WITH row WHERE row.Municipi <> 'null' AND row.Id_Llar <> 'null'
MERGE (h:HABITATGE {Municipi: row.Municipi, Id_Habitatge: row.Id_Llar, Any_Padro: toInteger(row.Any_Padro), Carrer: row.Carrer, 
Numero: CASE WHEN row.Numero <> "null" THEN toInteger(row.Numero) ELSE row.Numero END});


//INDIVIDUAL
LOAD CSV WITH HEADERS FROM 'file:///INDIVIDUAL.csv' AS row
WITH row WHERE row.Id <> "null"
MERGE (i:INDIVIDUAL {Id: row.Id})
SET i.Year = toInteger(row.Year), i.name = row.name, i.surname = row.surname, i.second_surname = row.second_surname;


//VIU
LOAD CSV WITH HEADERS FROM 'file:///VIU.csv' AS row
WITH row WHERE row.IND <> 'null' AND row.HOUSE_ID <> 'null'
// WITH row.IND AS IND, toInteger(row.HOUSE_ID) AS HOUSE_ID, row.Location AS Location, toInteger(row.Year) AS Year
MATCH (i:INDIVIDUAL), (h:HABITATGE)
WHERE row.IND = i.Id AND toInteger(row.HOUSE_ID) = toInteger(h.Id_Habitatge) AND toInteger(row.Year) = toInteger(h.Any_Padro)
MERGE (i)-[:VIU {Location: row.Location, Year: row.Year}]->(h);


//SAME_AS
LOAD CSV WITH HEADERS FROM 'file:///SAME_AS.csv' AS row
WITH row WHERE row.Id_A <> "null" AND row.Id_B <> "null"
MATCH (p:INDIVIDUAL {Id: row.Id_A}), (s:INDIVIDUAL {Id: row.Id_B})
MERGE (p)-[:SAME_AS]->(s);


//FAMILIA
LOAD CSV WITH HEADERS FROM 'file:///FAMILIA.csv' AS row
WITH row WHERE row.ID_1 <> "null" AND row.ID_2 <> "null"
MATCH (p:INDIVIDUAL), (s:INDIVIDUAL)
WHERE row.ID_1 = p.Id AND row.ID_2 = s.Id
CREATE (p)-[:FAMILIA {Relacio: row.Relacio, Relacio_Harmonitzada: row.Relacio_Harmonitzada}]->(s);